; Data used by routines in powerup

VDPSTACK	EQU	>8370

fbufheader
	DATA	>AA3F,>FF11
	BYTE	>03

sndflg
	BYTE	>01
	EVEN

upsnd
	BYTE	>04,>9F,>BF,>DF,>FF,>02		; quiet

	BYTE	>03,>80,>05,>94,>07
	BYTE	>03,>8B,>06,>94,>07
	BYTE	>03,>80,>05,>90,>0E

        BYTE	>01,>9F,>00			; terminate
	EVEN


; Power UP routine to reset TIPI latches.
onreset				; 
	STWP	R5		; Hold WP for EQU based offsets.
	LIMI	0

	CLR	@TDOUT		; Clear TIPI data output
	CLR	@TCOUT		; Clear control output

; trigger reset signal to RPi
	SBO	1		; turn on the second cru bit
	
; set a playlist
	LI	R2,>1700	; vdp create write address
	ORI	R2,VDWRITE
	LI	R1,VDPWA	; port to tell vdp where to write
	MOVB	@R2LB(R5),*R1
	MOVB	R2,*R1

	LI	R1,VDPWD	; port to write data into vdp
	CLR	R3
sndcpy	
	MOVB	@upsnd(R3),*R1
	INC	R3
	CI	R3,24
	JNE	sndcpy

	LI	R2,>1700
	MOV	R2,@>83CC	; set sound list address 
	MOVB	@sndflg,@>83CE	; enable playback
	SOCB	@sndflg,@>83FD

; turn off the reset signal so RPi service can finish starting
	SBZ	1

; setup disk buffers if crubase is that of floppy controller
	CI	R12,>1100
	JNE	done
	LI	R2,>37D7	; room for 3 file buffers
	MOV	R2,@VDPSTACK
	LI	R1,VDPWA
	INC	R2		; compose vdp write address
	ORI	R2,VDWRITE
	MOVB	@R2LB(R5),*R1
	MOVB	R2,*R1		; write address for vdp is set
	CLR	R2
fheadcopy
	MOVB	@fbufheader(R2),@VDPWD
	INC	R2
	CI	R2,5
	JNE	fheadcopy

; Return to TI startup
done
	LIMI	2
	RT			; return to console

